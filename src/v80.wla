; v80 CP/M (c) Kroc Camen 2023, MIT License
; a z80 assembler for Z80s
;
.INC    "cpm.wla"                       ; CP/M interface
.INC    "err.wla"                       ; error message handling
.INC    "z80.wla"                       ; static Z80 opcode binary tree

init:
;===============================================================================
        ; set heap position below stack:
        ;-----------------------------------------------------------------------
        ld      HL,     $0100           ; oddly, there's no `ld HL, SP`!
        add     HL,     SP              ; stack address, e.g. $FCFF
        ex      DE,     HL              ; swap SP into DE
        ld      A,      D               ; take high byte with 1 added
        ld      HL,     @stack          ; stack addr position in banner string
        call    @hex                    ; convert and insert

        dec     D                       ; drop down a page
        ld      A,      D
        ld      HL,     @heap           ; heap addr position in banner string
        call    @hex                    ; convert and insert
        
        dec     DE                      ; heap is post-decrement, unlike stack
        ld      [heap], DE              ; finally set the heap position

        ld      DE,     __code
        ld      HL,     @code           ; code addr position in banner string
        ld      A,      D
        call    @hex                    ; convert and insert
        inc     HL
        ld      A,      E
        call    @hex                    ; convert and insert

        ld      DE,     @banner         ; print the banner + memory status
        call    osPrintStr

        ; check for an automatic file-reference,
        ; i.e. a file parameter was passed on the command line
        ;-----------------------------------------------------------------------
        ; check filename first-letter in the built-in file-control-block
        ld      A,      [command_fcb.filename]
        cp      ' '+1                   ; non-visible char? $00-$20?
        jp      c,      help            ; show help msg and quit

        ; open file:
        xor     A                       ; start reading at record 0
        ld      [command_fcb.current_record],   A
        ld      C,      BDOS_OPEN
        ld      DE,     command_fcb     ; use default FileControlBlock
        call    BDOS
        and     A                       ; (set flags!)
        jp      m,      errFile         ; $FF = error

        ; assemble the file
        ;-----------------------------------------------------------------------
        call    assemble
        ;-----------------------------------------------------------------------

        ; return to CP/M handler to quit the program
        ret

        ;-----------------------------------------------------------------------
        ; convert A to ASCCI hexadecimal digits and insert in the banner string
        ;
@hex:   call    getHexByte
        ld      [HL],   B
        inc     HL
        ld      [HL],   C
        ret

        ;-----------------------------------------------------------------------
@banner .BYTE   "v80 CP/M v0, Kroc Camen 2023\n"
        .BYTE   "[stack: "
@stack  .BYTE   "0000"
        .BYTE   " | heap: "
@heap   .BYTE   "0000"
        .BYTE   " | code: "
@code   .BYTE   "0000"
        .BYTE   "]\n$"


.BLOCK  "code"
;===============================================================================
row:    .WORD $0000                     ; current line-number in file
col:    .BYTE $00                       ; current column-number in file
char:   .BYTE $00                       ; current character

param:  .BYTE $00                       ; next parameter properties

; heap:
;-------------------------------------------------------------------------------
; as assembly progresses, the dictionary of symbols and deferred expressions
; will be built up on the heap. it works downwards from the top of memory
; in order to maximise the available space before the assembled binary
; and heap collide. during initialisation the heap will be placed 256 bytes
; below the stack at the top of memory, giving enough room for the stack
;
heap:   .ADDR $fff                      ; current position on the heap
dict:   .ADDR $000                      ; addr of last dictionary-entry

; expression bytecode:
;-------------------------------------------------------------------------------
; calculations are compiled into bytecode to allow for later evaluation,
; i.e. for deferred calculations where a symbol is not yet known
;
.DEF    EXPR_END        0               ; expression terminator
.DEF    EXPR_NUMBER     '$'             ; hexadecimal literal
.DEF    EXPR_LABEL      ':'             ; a label (16-bit)
.DEF    EXPR_CONST      '#'             ; a constant (flexible size)
.DEF    EXPR_OP_LO      '<'             ; `<`: lo-byte operator
.DEF    EXPR_OP_HI      '>'             ; `>`: hi-byte operator
.DEF    EXPR_OP_NOT     '!'             ; `!`: not operator
.DEF    EXPR_OP_ADD     '+'             ; `+`: add operator 
.DEF    EXPR_OP_SUB     '-'             ; `-`: subtract operator
.DEF    EXPR_OP_MUL     '*'             ; `*`: multiply operator
.DEF    EXPR_OP_DIV     '/'             ; `/`: divide operator
.DEF    EXPR_OP_AND     '&'             ; `&`: and operator
.DEF    EXPR_OP_OR      '|'             ; `|`: or operator
.DEF    EXPR_OP_XOR     '^'             ; `^`: xor operator
.DEF    EXPR_OP_MOD     '%'             ; `%`: modulo operator

expr:   .ADDR $000                      ; addr of last deferred expression


assemble:
;===============================================================================
; assembles a file:
;
; TODO: do the file opening here so that we can do includes
;-------------------------------------------------------------------------------
        ; parse a word from the input file (root context)
        ;
        ld      HL,     [code]          ; where we'll be assembling to
@word:  call    skipWhitespace          ; find first character

        ; first character determines the word type:
        cp      ';'                     ; is comment?
        jr      z,      @skip           ; skip until end of line

        call    parseInstruction

        jr      @word

        ; skip comments
        ;-----------------------------------------------------------------------
@skip:  call    getChar                 ; read next character (returns z-flag)
        ret     z                       ; return C=0 for no word, end-of-file
        cp      ASCII_LF                ; is newline?
        jr      nz, @word               ; keep going until new line
        jr      @word                   ; try get next word


skipWhitespace:
;===============================================================================
; reads input until a visible character (ASCII >32) is reached:
; 
; out:  A               ASCII code, 0 for end-of-file
;-------------------------------------------------------------------------------
-       call    getChar                 ; read a character (returns z-flag)
        ret     z                       ; return z-flag set if end-of-file
        cp      ASCII_SPC+1             ; ignore any ASCII code <= $20 (spc)
        jr      c, -                    ; keep reading blank chars
        ret


parseInstruction:
;===============================================================================
; parse a Z80 instruction into opcodes:
;
; in:   HL              assembly addr to emit opcodes
; out:  HL              advanced to next [unused] addr
;       A|BC|DE         (clobbered)
;       IX|IY           (undefined)
;-------------------------------------------------------------------------------
.BLOCK  "opcode-parser"
        ld      DE,     opcodes         ; start at beginning of opcode tree

        ; the first character is already in A
        set     5,      A               ; force lowercase (see desc. below)
        jr      +                       ; jump into the parsing loop

        ;=======================================================================
        ; match; follow the branch
        ;-----------------------------------------------------------------------
@match: inc     DE                      ; step over the character to the addr
        ld      A,      [DE]            ; read the hi-byte / opcode-flags
        inc     DE                      ; move to lo-byte / opcode-byte

        ; is it a jump to another branch, or an opcode? (opcode-flags have
        ; to be in the hi-byte because a jump-address lo-byte could be >128)
        and     A                       ; (set flags)
        jp      m,      @opcode         ; high-bit set = this is an opcode

        ex      DE,     HL              ; swap DE|HL due to Z80 limitations
        ld      L,      [HL]            ; read lo-byte
        ld      H,      A               ; apply the hi-byte from before
        ex      DE,     HL              ; swap DE|HL back

        ; get character from input file:
        ;-----------------------------------------------------------------------
@char:  call    getChar                 ; read from input file
        cp      ASCII_SPC+1             ; is it whitespace? (hold carry...)

        ; force lowercase, without also affecting numbers/(most) punctuation:
        ;
        ; this essentially forces ASCII codes 64-95 (@A-Z[\]^_) to codes
        ; 96-127 (`a-z{|}~) which makes A-Z lowercase with the caveat that
        ; some punctuation cannot be differentiated "@"<->"`", "[]"<->"{}"",
        ; "\"<->"|" and "^"<->"~" but we aren't using any of those in the
        ; instruction names anyway
        ;
        ; it also means that ASCII codes 0-31 (non-visible) are promoted
        ; to 32-64 (visible), but we have already checked for ASCII codes
        ; 32 (space) or below and this is signalled by the carry flag; so
        ; even though the below instruction would change tab into ")", we
        ; will undo this afterwards
        ;
        set     5,      A               ; force partial lowercase
        jr      nc,     +               ; was this a non-visible char before?
        xor     A                       ; any whitespace = end-of-word (0)
+       ld      C,      A               ; set as character to compare against

        ;///////////////////////////////////////////////////////////////////////
        push    AF
        call    osPrintChar             ; DEBUG!
        pop     AF
        ;///////////////////////////////////////////////////////////////////////

        ; get character from opcode tree:
        ;-----------------------------------------------------------------------
@retry: ld      A,      [DE]            ; read from current position in tree
        and     A                       ; (set flags!)

        ; if the top-bit is set, this is either a continuation character
        ; (it must match and there is no branch) or an end of branch
        jp      m,      @cont

        ; does the character match the one in the opcode tree?
        cp      A,      C
        jr      z,      @match

        ; no match; try the next branch:
        ;-----------------------------------------------------------------------
@skip:  inc     DE
        inc     DE
        inc     DE
        jr      @retry

        ;-----------------------------------------------------------------------
        ; handle continuation character / end-of-branch:
        ;
        ; a continuation character has no branch, one character has to
        ; immediately follow another; any mismatch is an unknown opcode
        ;
        ; if, after removing the high-bit, the char is null, then we've reached
        ; the end of a branch and there are no more alternate paths to take.
        ; this is also an unknown opcode
        ;
@cont:  and     %01111111               ; remove top bit before comparison
        jp      z,      errOpcode       ; end of branch? (128 -> 0)
        cp      A,      C               ; compare with input char
        jp      nz,     errOpcode       ; if they don't match, error!
        inc     DE                      ; match -- move to next char in tree
        jr      @char

        ; emit opcode:
        ;-----------------------------------------------------------------------
@opcode ld      [param],        A       ; retain flags for parameter fetching
        ld      C,      A               ; retain the opcode-flags byte
        ld      A,      [DE]            ; read the opcode byte

        ; with the instruction matched to opcode, we no longer need
        ; the tree position; load up the virtual program-counter
        ld      DE,     [pc]

        ; the flags byte is a set of flags for Z80-specifics
        ; and what, if any, kind of parameter is required
        ;
        bit     4,      C               ; bit 4 indicates an IX instruction
        jr      z,      +
        ld      [HL],   $dd             ; emit IX prefix byte ($DD)
        inc     HL                      ; move to next byte in code segment
        inc     DE                      ; increment virtual program-counter

+       bit     6,      C               ; bit 6 indicates an IY instruction
        jr      z,      +
        ld      [HL],   $fd             ; emit IY prefix byte ($FD)
        inc     HL                      ; move to next byte in code segment
        inc     DE                      ; increment virtual program-counter

+       bit     3,      C               ; bit 3 indicates extended ($CB) opcode
        jr      z,      +               ; (these must appear after IX/IY bytes)
        ld      [HL],   $cb             ; emit $CB prefix byte
        inc     HL                      ; move to next byte in code segment
        inc     DE                      ; increment virtual program-counter

+       bit     5,      C               ; bit 5 indicates extended ($ED) opcode
        jr      z,      +               ; (these must appear after IX/IY bytes)
        ld      [HL],   $ed             ; emit $ED prefix byte
        inc     HL                      ; move to next byte in code segment
        inc     DE                      ; increment virtual program-counter
+

        ; TODO: DD|FB + CB opcodes put the opcode after the parameter!
        ; we'll have to reserve the parameter and return here

        ld      [HL],   A               ; now emit the opcode byte
        inc     HL

        ld      [code], HL              ; write back code segment position
        ld      [pc],   DE              ; write back virtual program-counter

        ; handle parameters:
        ;-----------------------------------------------------------------------
        call    parseParameter

        ;///////////////////////////////////////////////////////////////////////
        ld      A,      '-'
        call    osPrintChar             ; DEBUG!
        ;///////////////////////////////////////////////////////////////////////

        ld      HL,     [code]
        ret
.ENDB

parseParameter:
;===============================================================================
; reserves bytes for a parameter value and parses the expression that follows;
; deferring evaluation
;-------------------------------------------------------------------------------

pushExpression:
;===============================================================================
; append an expression to the heap:
;
;-------------------------------------------------------------------------------
        ; add an expression to the heap,
        ; this requires:
        ;
        ; - the link addr to the previous expression
        ; - the current code-segment address
        ; - the size of the output; 1 or 2 bytes
        ; - the current virtual program-counter
        ;   (in case of relative jump)
        ; - the expression (as bytecode)
        ;
        ; link this expression to the previous:
        ; write the previous expr addr to the head of this one
        ; (preserving the previous addr)
        ;
        ld      DE,     [heap]          ; get current heap position
        ld      HL,     expr            ; addr to read bytes from
        ldi                             ; (copy bytes across)
        ldi                             ; [DE]=[HL], DE++, HL++, BC--

        ; update the last expression field to point to this expression:
        ; warning: DE has already been advanced (by LDI), so we need to
        ; get the heap address again (without resetting DE)
        ld      HL,     [heap]          ; start addr of our expression
        ld      [expr], HL              ; write to last expression addr

        ; write the current code-segment position;
        ; this is where the calculation result will be filled in
        ;
        ld      HL,     code            ; current code segment position
        ldi                             ; (copy bytes across)
        ldi                             ; [DE]=[HL], DE++, HL++, BC--

        ; set the size/type of the deferred result
        ld      HL,     param           ; parameter-type byte
        ldi                             ; [DE]=[HL], DE++, HL++, BC--

        ; add the current virtual program-counter
        ; (for calculating relative jumps)
        ld      HL,     pc              ; virtual program-counter
        ldi                             ; (copy bytes across)
        ldi                             ; [HL]=[DE], HL++, DE++, BC--

        ; add the row/column of the expression in case of
        ; error when we come back to it later
        ld      HL,     row             ; current input file row number
        ldi                             ; copy low byte,
        ldi                             ;  hi byte,
        ldi                             ; and the column number that follows

        ; reserve the correct number of bytes for the result


        ; begin parsing the expression, packing it into bytecode
        ;-----------------------------------------------------------------------
        ex      DE,     HL              ; (swap the current heap addr into HL)
        call    parseExpr

        ; attempt to evaluate the expression
        ; ...
        ld      [heap], HL

        ret


-       inc     HL                      ; (move to next byte on heap)

parseExpr:
;===============================================================================
; parse an expression from the input file:
;
; words are read and converted into tokens to evaluate later
; (e.g. deferred expression where a symbol is not yet known)
;
; this is different from regular parsing as it does not write
; to the code segment and opcodes are not permitted
;
; in:   HL              address on the heap to begin writing expr bytecode
;-------------------------------------------------------------------------------
        call    skipWhitespace          ; read first character of the next word
        jp      z,      errEndOfFile    ; handle unexpected end of file!

        ; an expression must begin with either a value
        ; or a unary operator + a value
@operand:
        ; unary operator?
        ;-----------------------------------------------------------------------
        ; unary operators can always precede values, even after an operator
        ; e.g. `$10 + <:label`
        ;
        cp      '<'                     ; lo-byte operator?
        jr      nz,     +
        ld      [HL],   A               ; push a lo-byte operator token
        jr      @unary                  ; a value must follow a unary operator
        ;-----------------------------------------------------------------------
+       cp      '>'                     ; hi-byte operator?
        jr      nz,     +
        ld      [HL],   A               ; push a hi-byte operator token
        jr      @unary                  ; a value must follow a unary operator
        ;-----------------------------------------------------------------------
+       cp      '!'                     ; not operator?
        jr      nz,     +               ; no unary operators, check for values
        ld      [HL],   A               ; push a not operator token

        ; a unary operator has been added,
        ; read the next character:
@unary: inc     HL                      ; (move to next byte on heap)
        call    skipWhitespace          ; read first character of the next word
        jp      z,      errEndOfFile    ; handle unexpected end of file!

+       ; hexadecimal literal?
        ;-----------------------------------------------------------------------
        ; a sigil is required because A-F can't be separated
        ; from instruction names without look-ahead
        ;
@value: cp      '$'                     ; hexadecimal sigil?
        jr      nz,     +

        ld      [HL],   A               ; push a number token
        inc     HL
        call    parseHexNumber          ; parse the hex number into BC
        ld      [HL],   C
        inc     HL
        ld      [HL],   B
        inc     HL
        
        jr      @operator               ; an operator can follow a value

        ; constant?
        ;-----------------------------------------------------------------------
+       cp      '#'                     ; a constant?
        jr      nz,     +

        halt

        ; label?
        ;-----------------------------------------------------------------------
+       cp      ':'                     ; a label?
        jp      nz,     errExpr         ; expression has no value!!
        
        halt

        ;-----------------------------------------------------------------------
        ; a value has been added, check for the presence of an operator:
        ; if there's no operator, the expression ends here
@operator:
        call    skipWhitespace          ; read first character of the next word
        jr      z,      @end            ; if end-of-file, end the expression

        cp      '+'                     ; add operator?
        jr      nc,     +
        ld      [HL],   A               ; push an ADD operator token
        jr      -
        ;-----------------------------------------------------------------------
+       cp      '-'                     ; subtract operator?
        jr      nc,     +
        ld      [HL],   A               ; push a SUB operator token
        jr      -
        ;-----------------------------------------------------------------------
+       cp      '*'                     ; multiply operator?
        jr      nc,     +
        ld      [HL],   A               ; push a MUL operator token
        jr      -
        ;-----------------------------------------------------------------------
+       cp      '/'                     ; divide operator?
        jr      nc,     +
        ld      [HL],   A               ; push a DIV operator token
        jr      -
        ;-----------------------------------------------------------------------
+       cp      '&'                     ; and operator?
        jr      nc,     +
        ld      [HL],   A               ; push an AND operator token
        jr      -
        ;-----------------------------------------------------------------------
+       cp      '|'                     ; or operator?
        jr      nc,     +
        ld      [HL],   A               ; push an OR operator token
        jr      -
        ;-----------------------------------------------------------------------
+       cp      '^'                     ; xor operator?
        jr      nc,     +
        ld      [HL],   A               ; push a XOR operator token
        jr      -
        ;-----------------------------------------------------------------------
+       cp      '%'                     ; modulo operator?
        jr      nc,     @end

        ld      [HL],   A               ; push a MOD operator token
        jr      -

        ;-----------------------------------------------------------------------
        ; whatever it is, it isn't an operator;
        ; fall through and terminate the expression
        ;
@end:   ld      [HL],   EXPR_END        ; terminate the expression
        inc     HL
        ret


parseHexNumber:
;===============================================================================
; parses an ASCII hexadecimal number and returns the value:
;
; out:  BC              the value, 0 padded for numbers < 256
;       A, HL           (clobbered)
;-------------------------------------------------------------------------------
        ld      C,     0                ; return value will go here

        ; try to read up to 4 digits; the 5th character
        ; must be whitespace or end-of-file!
        ;
        ld      B,      5
        ld      HL,     @num            ; where to write the bytes
-       call    osGetChar               ; read a char from file
        cp      33                      ; ASCII space or below?
        jr      c,      +               ; stop when reaching whitespace
        ld      [HL],   A               ; append byte
        inc     HL                      ; space for next digit
        djnz    -                       ; decrement B, loop until 0

        ; if a 5th byte was read that wasn't whitespace,
        ; it's an error -- the number is too long!
        jp      errHexNumber

        ;-----------------------------------------------------------------------
+       ld      HL,     @num            ; go back to the start of the number
        
        ; number of digits in hex number is reversed;
        ; B=1 for 4 digits, B=2 for 3 digits, B=1 for 2 digits and B=0 for 1
        ;
        dec     B                       ; 4 digits?
        jr      z,      @four
        dec     B                       ; 3 digits?
        jr      z,      @three
        dec     B                       ; 2 digits?
        jr      z,      @two
        dec     B                       ; B=0 for 1-byte hex number in C
        jr      z,      @one

        ; if there were _no_ digits after the hexadecimal
        ; sigil ("$") then B was 5 and we error
        jp      errHexNumber

        ; four-digit hex number:
        ;-----------------------------------------------------------------------
        ; a 4th digit must go in the upper nybble of B:
        ;
@four:  ld      A,      [HL]            ; get digit
        call    @digit                  ; convert digit to number
        add     A,      A               ;
        add     A,      A               ; left-shift (faster than `sla A`)
        add     A,      A               ; (shift lo-nybble to hi-nybble)
        add     A,      A               ;
        ld      B,      A               ; answer goes in B
        inc     HL                      ; move to 3rd digit
        ;-----------------------------------------------------------------------
        ; a 3rd digit goes in the lower nybble of B:
        ;
@three: ld      A,      [HL]            ; get digit
        call    @digit                  ; convert digit to number
        or      B                       ; combine with hi-nybble (if present)
        ld      B,      A               ; answer goes in B
        inc     HL                      ; move to 2nd digit
        ;-----------------------------------------------------------------------
        ; a 2nd digit goes in the upper nybble of C:
        ;
@two:   ld      A,      [HL]            ; get digit
        call    @digit                  ; convert digit to number
        add     A,      A               ;
        add     A,      A               ; left-shift (faster than `sla A`)
        add     A,      A               ; (shift lo-nybble to hi-nybble)
        add     A,      A               ;
        ld      C,      A               ; answer goes in C
        inc     HL                      ; move to 1st digit
        ;-----------------------------------------------------------------------
        ; 1st digit goes in the lower nybble of C:
        ;
@one:   ld      A,      [HL]            ; get digit
        call    @digit                  ; convert digit to number
        or      C                       ; combine with hi-nybble (if present)
        ld      C,      A               ; answer goes in C

        ret                             ; ship it!

        ; TODO: so long as these bytes don't cross a page-boundary 
        ;       ($xxFF->$xx00) we could change `inc HL` for `inc L`
@num:   .BYTE   $00, $00, $00, $00, $00

        ; convert a digit to a number:
        ;=======================================================================
        ; whitespace must already be precluded. err if not hex-digit
        ;
        ; valid hex digit?
        ;
        ; if we unset bit 5 then ASCII "a"-"f" (97-102) becomes "A"-"F" (65-70)
        ; without also altering existing uppercase letters. This also has the
        ; side-effect of changing ASCII "0"-"9" (30-39) to codes 16-25!
        ; (this is why ASCII codes 0-32 must be precluded beforehand)
        ;
@digit: res     5,      A               ; force upper-case for "a"-"f"
        sub     16                      ; anything < "0" becomes >=128 (hi-bit)
        cp      10                      ; 0-9 ("0"-"9") is okay
        ret     c                       ; exit early for 0-9

        ; between ASCII "9" and "A" is ":", ";", "<", "=", ">", "?" and "@";
        ; if we subtract 7, "A" becomes 0 and any of those 7 become negative
        ; (>=128, hi-bit set), therefore we only need to check 0-5 ("A"-"F")
        sub     7
        cp      6                       ; 0-5 ("A"-"F") are allowed
        jp      nc,     errHexNumber    ; error for anything above "F"
        add     10                      ; rebase "A" to 10, "F" to 15
        ret


getChar:
;===============================================================================
; reads the next character in the file.
; current row and column are maintained
;
; TODO: handle tab (next 8th char boundary)
;
; out:  A                               ASCII code, 0 for end-of-file
;       z                               zero-flag is set according to A
;       DE, HL                          (preserved)
;       BC, IX/Y                        (undefined)
;-------------------------------------------------------------------------------
-       call    osGetChar               ; read a char from file
        ld      [char], A               ; retain returned character
        ret     z                       ; return on null byte (end-of-file)

        ; check for end of line:
        ;-----------------------------------------------------------------------
        cp      ASCII_CR                ; /r is always ignored,
        jr      z, -                    ;  don't advance row/col, get next char

        exx                             ; must preserve HL & DE
        ld      HL',    col             ; prepare to increment column number

        cp      ASCII_LF                ; /n is newline
        jr      nz,     @col            ; if not, skip to next column

        ; increment line-number:
        ;-----------------------------------------------------------------------
        ld      DE',    [row]           ; 16-bit load,
        inc     DE'                     ;  and increment,
        ld      [row],  DE'             ;  and write
        ld      [HL'],  0               ; reset column number

        ; increment column number:
        ;-----------------------------------------------------------------------
        ; (capped at 255 even though lines can be longer)
@col:   inc     [HL']
        jr      nz,     +               ; if roll over, roll back (less
        dec     [HL']                   ;  instructions than read+add+write)

+       exx                             ; restore HL/DE
        and     A                       ; return z-flag
        ret


printHexByte:
;===============================================================================
; print a byte as hexadecimal:
;
; in:   A               byte to print
; out:  A, BC           (clobbered)
;       DE|HL|IXY       (undefined) -- depends on osPrintChar
;
; NOTE: multiple online sources cite this method, dating back to the 70s!
;-------------------------------------------------------------------------------
        call    getHexByte
        ld      A,      B
        call    osPrintChar
        ld      A,      C
        jp      osPrintChar

getHexByte:
;===============================================================================
; convert a byte into two ASCII hexadecimal digits:
;
; in:   A               input byte
; out:  BC              ASCII hex digits in hi,lo (printable) order
;       DE|HL|IX/Y      (preserved)
;       A               (clobbered)
;-------------------------------------------------------------------------------
        ld      B,      A               ; preserve for 2nd digit
        rra                             ;
        rra                             ; shift upper nybble down
        rra                             ;
        rra                             ;
        call    @digit                  ; convert to ASCII
        ld      A,      B               ; retrieve 2nd digit
        ld      B,      C               ; set upper ASCII digit

        ; fallthrough to convert 2nd digit
        ; ...

@digit: and     %00001111               ; isolate low-nybble
        add     $90                     ; magic!
        daa                             ; adjust to decimal
        adc     $40                     ; more magic!
        daa                             ; adjust to decimal
        ld      C,      A
        ret
.ENDB

; * * *   O B J E C T   C O D E   * * *
;===============================================================================
; this is where the assembled code will be placed;
; anything beyond this point will be overwritten
;
pc:     .WORD $0000                     ; assembly virtual program-counter
code:
;-------------------------------------------------------------------------------
        .WORD __code                    ; pointer to current position

__code: ; WARNING: ANYTHING PLACED AFTER HERE WILL BE OVERWRITTEN

;///////////////////////////////////////////////////////////////////////////////

help:
;===============================================================================
; print the help string:
;
; this isn't needed once assembly starts, so it gets overwritten
;-------------------------------------------------------------------------------
        ld      DE,     @str
        jp      osPrintStr

@str:   .BYTE "\n    V80 SOURCE.V80$"